plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'org.querqy'
version '0.2.0'

repositories {
    mavenCentral()
}

ext {
    luceneSolrVersion = '8.11.2'
}

dependencies {
    compileOnly "org.apache.lucene:lucene-core:${luceneSolrVersion}"
    compileOnly "org.apache.solr:solr-core:${luceneSolrVersion}"

    testImplementation "org.querqy:querqy-solr:5.4.lucene810.0"
    testImplementation "org.apache.solr:solr-test-framework:${luceneSolrVersion}"
    testImplementation "org.assertj:assertj-core:3.22.0"
    testImplementation "junit:junit:4.11"
    testImplementation project(":library")

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

}

test {
    useJUnit()
    systemProperty 'test.solr.allowed.securerandom', 'NativePRNG'
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = 11
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.querqy'
            artifactId = 'querqy-unplugged-solr'
            version = '0.2.0'

            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Querqy Unplugged Solr'
                description = 'A lightweight Solr plugin that allows the usage of some lucene queries via Solr JSON DSL'
                url = 'https://querqy.org'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jope'
                        name = 'Johannes Peter'
                        email = 'johannesdaniel.peter@gmx.de'
                    }
                }
                scm {
                    url = 'git@github.com:querqy/querqy-unplugged.git'
                    connection = 'scm:git:git@github.com:querqy/querqy-unplugged.git'
                    developerConnection = 'scm:git:git@github.com:querqy/querqy-unplugged.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'

            name = 'sonatype'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
